#version 460
#extension GL_NV_ray_tracing : require

layout(set = 0, binding = 0) uniform Scene {
    mat4 model;
    mat4 view;
    mat4 view_inverse;
    mat4 projection;
    mat4 projection_inverse;
    mat4 model_view_projection;
    vec3 frame;
} scene;
layout(set = 1, binding = 0) uniform accelerationStructureNV topLevelAS;
layout(set = 1, binding = 1, rgba8) uniform image2D image;

layout(location = 0) rayPayloadNV vec3 hitValue;

void main() 
{
    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeNV.xy);
    vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = scene.view_inverse * vec4(0,0,0,1);
	vec4 target = scene.projection_inverse * vec4(d.x, d.y, 1, 1);
	vec4 direction = scene.view_inverse * vec4(normalize(target.xyz), 0);

    hitValue = direction.xyz;

    uint rayFlags = gl_RayFlagsOpaqueNV;
    uint cullMask = 0xff;
    float tmin = 0.001;
    float tmax = 1000.0;
    traceNV(topLevelAS, rayFlags, cullMask, 0 /*sbtRecordOffset*/, 0 /*sbtRecordStride*/, 0 /*missIndex*/, origin.xyz, tmin, direction.xyz, tmax, 0 /*payload*/);

    imageStore(image, ivec2(gl_LaunchIDNV.xy), vec4(hitValue, 0.0));
}